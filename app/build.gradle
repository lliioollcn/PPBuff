import cn.lliiooll.dhl.plugin.Common

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'cn.lliiooll.dhl.plugin'
}

android {
    namespace 'cn.lliiooll.ppbuff'
    compileSdk 33

    defaultConfig {
        applicationId "cn.lliiooll.ppbuff"
        minSdk 24
        targetSdk 33
        if (System.getenv("KEYSTORE_PATH") != null) {
            versionCode Common.getBuildVersionCode(rootProject)
        } else {
            versionCode 10086
        }
        versionName "1.0.0${Common.getGitHeadRefsSuffix(rootProject)}"
        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    packagingOptions {
        jniLibs.excludes.add("lib/**/liblog.so")
        jniLibs.excludes.add("lib/**/libz.so")
        jniLibs.excludes.add("lib/x86/**")
        jniLibs.excludes.add("lib/x86_64/**")
        resources.excludes.addAll(
                "META-INF/**",
                "kotlin/**",
                "**.bin",
                "kotlin-tooling-metadata.json"
        )
    }


    if (System.getenv("KEYSTORE_PATH") != null && System.getenv("KEY_ALIAS") != null) {
        signingConfigs {
            release {
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
                storeFile file(System.getenv("KEYSTORE_PATH"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                enableV2Signing = true
                enableV3Signing = true
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    buildTypes {
        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }

    androidResources {
        additionalParameters("--no-version-vectors", "--allow-reserved-package-id", "--package-id", "0xcf")
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    buildToolsVersion '33.0.1'
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    //noinspection GradleDependency
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    //noinspection GradleDependency
    implementation 'androidx.compose.material3:material3:1.1.0-alpha04'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation 'androidx.documentfile:documentfile:1.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    compileOnly 'de.robv.android.xposed:api:82'

    implementation 'com.github.kyuubiran:EzXHelper:1.0.3'
    implementation 'org.luckypray:DexKit:1.1.0'
    implementation 'com.tencent:mmkv:1.2.15'
    //implementation 'com.google.code.gson:gson:2.10.1'
    //implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'cn.hutool:hutool-all:5.8.11'


    def appCenterSdkVersion = '4.4.5'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"


}